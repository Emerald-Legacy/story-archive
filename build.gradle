import org.asciidoctor.gradle.jvm.AsciidoctorTask

plugins {
    id 'org.asciidoctor.jvm.base' version '4.0.4'
    id 'org.asciidoctor.jvm.convert' version '4.0.4'
    id 'org.asciidoctor.jvm.pdf' version '4.0.4'
}

repositories {
    mavenCentral()
}

asciidoctorj {
    version = '2.5.12'
}

abstract class RenderStoryTask extends AsciidoctorTask {

    @Inject
    RenderStoryTask(WorkerExecutor we) {
        super(we)

        sourceDir = new File('./docs/')
        baseDir = new File('./docs/')

        outputDir = new File('./build/')
        outputOptions {
            separateOutputDirs = false
            backends 'pdf', 'html'
        }

        attributes([
            'icons'        : 'font',
            'revnumber'    : '',
            'revdate'      : '',
            'version-label': '',
            'language'     : 'EN',
            'imagesdir'    : 'images',
            'pdf-themesdir': '../pdf-theme/themes',
            'pdf-fontsdir' : '../pdf-theme/fonts',
            'pdf-theme'    : 'el'
        ])
    }
}

def renderStories = tasks.register('renderStories', RenderStoryTask) {
    group = 'Stories'
    description = 'Render all story files to PDF and HTML'
    sources {
        include 'stories/*.adoc'
    }
}

def renderChapterParts = tasks.register('renderChapterParts', RenderStoryTask) {
    group = 'Stories'
    description = 'Render all chapter part files to PDF and HTML'
    sources {
        include 'stories/chapter_parts/*.adoc'
    }
}

def renderIndex = tasks.register('renderIndex', RenderStoryTask) {
    group = 'Stories'
    description = 'Render the index file to PDF and HTML'
    sources {
        include 'index.adoc'
    }
}

tasks.register('buildStoryArchive') {
    group = 'Stories'
    description = 'Build all story files (index, stories, and chapter parts)'
    dependsOn renderStories, renderChapterParts, renderIndex
}

defaultTasks 'buildStoryArchive'
