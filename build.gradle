import org.asciidoctor.gradle.jvm.AsciidoctorTask
import java.text.SimpleDateFormat

plugins {
    id "org.asciidoctor.jvm.base" version "4.0.4"
    id "org.asciidoctor.jvm.convert" version "4.0.4"
    id "org.asciidoctor.jvm.pdf" version "4.0.4"
}
repositories {
    mavenCentral()
}

asciidoctorj {
    version = '2.5.12'
}

class RenderStoryTask extends AsciidoctorTask {
    @Inject
    RenderStoryTask(WorkerExecutor we, String fileName) {
        super(we)

        forkOptions {
            jvmArgs "--add-opens", "java.base/sun.nio.ch=ALL-UNNAMED", "--add-opens", "java.base/java.io=ALL-UNNAMED"
        }

        sourceDir = new File("./docs/")
        baseDir = new File("./docs/")
        sources {
            include fileName
        }

        outputDir = new File("./build/")
        outputOptions {
            separateOutputDirs = false
            backends 'pdf' // , 'html'
        }

        attributes = [
                'icons'        : 'font',
                'revnumber'    : '',
                'revdate'    : '',
                'version-label': '',
                'language'     : 'EN',
                'imagesdir'    : 'images',
                'pdf-themesdir': '../pdf-theme/themes',
                'pdf-fontsdir' : '../pdf-theme/fonts',
                'pdf-theme'    : 'el'
        ]
    }
}

tasks.register('buildStoryArchive') {
    group 'Stories'
    description 'Grouping task for generating all story files'
    dependsOn "renderChapterParts", "renderStories", "renderIndex"
}

task renderStories(type: RenderStoryTask,
        constructorArgs: ["stories/*.adoc"]) {
}

task renderChapterParts(type: RenderStoryTask,
        constructorArgs: ["stories/chapter_parts/*.adoc"]) {
}

task renderIndex(type: RenderStoryTask,
        constructorArgs: ["index.adoc"]) {
}


defaultTasks "buildStoryArchive"
